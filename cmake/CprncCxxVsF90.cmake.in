# Run cprnc on each of the output files against the target
# Note: we mix @ and $ notation, since at configure time we only
#       resolve @ variables. The remaning ones will be resolve by
#       cmake when the script is run with 'cmake -P CprncCxxVsF90.cmake'
FOREACH (NC_FILE_NAME @NC_OUTPUT_FILES@)
  EXECUTE_PROCESS (COMMAND  @CPRNC_BINARY@ @F90_DIR@/${NC_FILE_NAME} @CXX_DIR@/${NC_FILE_NAME}
                   RESULT_VARIABLE ERROR_CODE
                   OUTPUT_VARIABLE cprnc_stdout
                   ERROR_VARIABLE cprnc_stderr)
  MESSAGE (${cprnc_stdout})

  # Search stdout for "IDENTICAL". -1 means it does not exist. Use REVERSE on
  # the off chance that makes it faster, since "IDENTICAL", if it exists, is
  # near the end of the output.
  string (FIND "${cprnc_stdout}" "IDENTICAL" identical_pos REVERSE)

  # If cprnc does not return 0, then the test failed
  IF (identical_pos EQUAL -1)
    MESSAGE (FATAL_ERROR "Test did not succeed")
  ENDIF()
ENDFOREACH()
